plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.0.4'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'ru.vyarus.quality' version '2.0.0'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.jfrog.bintray' version '1.7.1'
    id 'net.researchgate.release' version '2.4.1'
    id 'com.github.ben-manes.versions' version '0.13.0'
    id "io.spring.dependency-management" version "0.6.0.RELEASE"
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = 3.0
}

ext {
    dropwizard = '1.0.0'
    guice = '4.1.0'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencyManagement {
    imports {
        mavenBom "io.dropwizard:dropwizard-bom:$dropwizard"
        mavenBom "com.google.inject:guice-bom:$guice"
    }
}
dependencies {
    provided 'junit:junit'
    provided 'io.dropwizard:dropwizard-testing'
    provided 'org.spockframework:spock-core:1.0-groovy-2.4'
    provided 'com.google.code.findbugs:annotations:3.0.1'

    compile 'com.google.inject:guice'
    compile 'com.google.inject.extensions:guice-servlet'
    compile 'com.google.inject.extensions:guice-multibindings'
    compile 'io.dropwizard:dropwizard-core'
    compile "org.glassfish.hk2:guice-bridge:2.4.0-b34"
    compile 'ru.vyarus:generics-resolver:2.0.1'

    testCompile 'io.dropwizard:dropwizard-auth'
    testCompile 'org.glassfish.jersey.ext:jersey-proxy-client'
    testCompile 'cglib:cglib-nodep:3.2.0'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
}

group = 'ru.vyarus'
description = 'Dropwizard guice integration'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['dropwizard', 'guice']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed', 'standard_error'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true

task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') << {
    java.awt.Desktop.getDesktop().open(file('/build/reports/project/dependencies/root.html'))
}
