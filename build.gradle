plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.0.5'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'ru.vyarus.quality' version '2.4.0'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.ben-manes.versions' version '0.15.0'
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '4.1'
}

ext {
    dropwizard = '1.1.3'
    guice = '4.1.0'
    hk2 = '2.5.0-b32'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencyManagement {
    imports {
        mavenBom "com.google.inject:guice-bom:$guice"
        mavenBom "io.dropwizard:dropwizard-bom:$dropwizard"
    }
    // exclusions here mostly fixes conflicts for maven projects
    dependencies {
        // force guava version from dropwizard bom
        dependency "com.google.guava:guava:${dependencyManagement.importedProperties['guava.version']}"
        dependency "org.glassfish.hk2:guice-bridge:$hk2", {
            exclude 'com.google.inject:guice'
            exclude 'org.glassfish.hk2:hk2-api'
        }
        dependency 'org.spockframework:spock-core:1.1-groovy-2.4'
        dependency 'com.github.stefanbirkner:system-rules:1.16.1'

        dependency "com.google.inject:guice:$guice", { exclude 'com.google.guava:guava' }
    }
}
dependencies {
    provided 'junit:junit'
    provided 'io.dropwizard:dropwizard-testing'
    provided 'org.spockframework:spock-core'
    provided 'com.github.stefanbirkner:system-rules'
    provided 'com.google.code.findbugs:annotations:3.0.1'
    provided "org.glassfish.hk2:guice-bridge"

    compile 'com.google.inject:guice'
    compile 'com.google.inject.extensions:guice-servlet'
    compile 'com.google.inject.extensions:guice-multibindings'
    compile 'io.dropwizard:dropwizard-core'
    compile 'ru.vyarus:generics-resolver:2.0.1'

    testCompile 'io.dropwizard:dropwizard-auth'
    testCompile 'org.glassfish.jersey.ext:jersey-proxy-client'
    testCompile 'cglib:cglib-nodep:3.2.0'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
}

group = 'ru.vyarus'
description = 'Dropwizard guice integration'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    delegate.properties {
        'guice.version' guice
        'dropwizard.version' dropwizard
        'hk2.version' hk2
    }
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['dropwizard', 'guice']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed', 'standard_error'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true

task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}
